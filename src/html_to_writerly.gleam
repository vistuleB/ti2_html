import io_lines as io_l
import gleam/int
import gleam/io
import gleam/list
import gleam/option.{type Option}
import gleam/pair
import gleam/result
import gleam/string
import html_pipeline
import infrastructure as infra
import simplifile
import vxml.{type VXML} as vp
import desugaring as vr
import writerly as wp
import on

const ins = string.inspect

fn each_prev_next(
  l: List(a),
  prev: Option(a),
  f: fn(a, Option(a), Option(a)) -> b,
) -> Nil {
  case l {
    [] -> Nil
    [first, ..rest] -> {
      let next = case rest {
        [] -> option.None
        [next, ..] -> option.Some(next)
      }
      f(first, prev, next)
      each_prev_next(rest, option.Some(first), f)
    }
  }
}

fn remove_0_at_start(s: String) -> String {
  case string.starts_with(s, "0") {
    True -> string.drop_start(s, 1)
    False -> s
  }
}

fn splitter(vxml: VXML, file: String) -> Result(List(vr.OutputFragment(Nil, VXML)), a) {
  let wly_file = file |> string.drop_end(5) <> ".wly"
  Ok([vr.OutputFragment(Nil, wly_file, vxml)])
}

fn remove_line_break_from_end(res: String) -> String {
  case string.ends_with(res, "\n") {
    True -> string.drop_end(res, 2)
    False -> res
  }
}

fn remove_line_break_from_start(res: String) -> String {
  case string.starts_with(res, "\n") {
    True -> string.drop_start(res, 1)
    False -> res
  }
}

fn title_from_vxml(vxml: VXML) -> String {
  let assert vp.V(_, _, _, title) = vxml
  wp.vxmls_to_writerlys(title)
  |> wp.writerlys_to_string()
  |> string.split_once(" ")
  |> result.unwrap(#("", ""))
  |> pair.second()
  |> remove_line_break_from_start
  |> remove_line_break_from_end
}

fn get_title_internal(vxml: VXML) -> String {
  case vxml {
    vp.T(_, _) -> ""
    vp.V(_, _, _, children) -> {
      case
        infra.v_children_with_class(vxml, "subChapterTitle"),
        infra.v_children_with_class(vxml, "chapterTitle")
      {
        [found, ..], _ -> title_from_vxml(found)
        _, [found, ..] -> title_from_vxml(found)
        _, _ -> get_title(children)
      }
    }
  }
}

fn get_title(vxmls: List(VXML)) -> String {
  case vxmls {
    [] -> ""
    [first, ..rest] -> {
      let title = get_title_internal(first)
      case title |> string.is_empty() {
        True -> get_title(rest)
        False -> title
      }
    }
  }
}

fn emitter(
  fragment: vr.OutputFragment(Nil, VXML),
  _prev_file: Option(String),
  _next_file: Option(String),
) -> Result(vr.OutputFragment(Nil, List(io_l.OutputLine)), String) {
  let vr.OutputFragment(_, filename, vxml) = fragment
  let title_en =
    filename
    |> string.drop_end(4)
    |> string.split("-")
    |> list.drop(2)
    |> string.join(" ")
  let title_german = get_title_internal(vxml)
  let chapter_number_as_string =
    filename |> string.split_once("-") |> result.unwrap(#("", "")) |> pair.first
  let assert True = chapter_number_as_string != ""
  // let number =
  //   filename
  //   |> string.split("-")
  //   |> list.take(2)
  //   |> list.map(remove_0_at_start)
  //   |> string.join(".")
  let assert [chapter_number, section_number] =
    filename
    |> string.split("-")
    |> list.take(2)
    |> list.map(remove_0_at_start)
    |> list.map(int.parse)
    |> list.map(result.unwrap(_, -1))
  let assert True = chapter_number >= 1
  let assert True = section_number >= 0
  let chapter_directory = "wly_content/" <> chapter_number_as_string

  case section_number == 0 {
    False -> Nil
    True -> {
      let _ = simplifile.create_directory(chapter_directory)
      let assert Ok(_) =
        simplifile.write(
          chapter_directory <> "/" <> "__parent.wly",
          "|> Chapter
    counter=SectionCtr
    title_gr="
          <> title_german
          <> "\n    title_en="
          <> title_en,
        )
      Nil
    }
  }

  // let root =
  //   vp.V(
  //     blame_us("Root"),
  //     "section",
  //     [
  //       vp.Attr(blame_us("section title"), "title_gr", title_german),
  //       vp.Attr(blame_us("section title"), "title_en", title_en),
  //       vp.Attr(blame_us("section title"), "number", number),
  //       // Counter attributes
  //       vp.Attr(blame_us("section def counter"), "counter", "DefCtr"),
  //       vp.Attr(blame_us("section exo counter"), "counter", "ExoCtr"),
  //     ],
  //     [construct_left_nav(prev_file), construct_right_nav(next_file), vxml],
  //   )

  let writerlys = wp.vxml_to_writerlys(vxml)

  Ok(vr.OutputFragment(
    classifier: Nil,
    path: chapter_directory <> "/" <> filename,
    payload: wp.writerlys_to_output_lines(writerlys),
  ))
}

fn drop_slash_at_end(path: String) -> String {
  case string.ends_with(path, "/") {
    True -> string.drop_end(path, 1)
    False -> path
  }
}

fn directory_files_else_file(
  path: String,
) -> Result(#(String, List(String)), simplifile.FileError) {
  case simplifile.read_directory(path) {
    Ok(files) -> {
      let path = drop_slash_at_end(path)
      Ok(#(path, files))
    }
    Error(_) -> {
      case simplifile.is_file(path) {
        Error(e) -> Error(e)
        _ -> {
          let assert Ok(#(reversed_filename, reversed_path)) =
            path |> string.reverse |> string.split_once("/")
          Ok(
            #(reversed_path |> string.reverse, [
              reversed_filename |> string.reverse,
            ]),
          )
        }
      }
    }
  }
}

fn html_purifying_assembler(
  path: String,
) -> Result(#(List(io_l.InputLine), option.Option(a)), simplifile.FileError) {
  use content <- on.ok(simplifile.read(path))
  let lines = 
    content
    |> vp.bad_html_pre_processor
    |> io_l.string_to_input_lines(path, 0)
  Ok(#(lines, option.None))
}

pub fn html_to_writerly(
  path: String,
  amendments: vr.CommandLineAmendments,
) -> Nil {
  use #(dir, files) <- on.error_ok(
    directory_files_else_file(path),
    fn(e) { io.print("failed to load files from " <> path <> ": " <> ins(e)) },
  )

  let files =
    files
    |> list.filter(string.ends_with(_, ".html"))
    |> list.sort(string.compare)

  each_prev_next(files, option.None, fn(file, prev, next) {
    let path = dir <> "/" <> file
    use <- on.eager_false_true(
      amendments.only_paths
      |> list.any(fn(f) { string.contains(path, f) || string.is_empty(f) })
        || list.is_empty(amendments.only_paths),
      Nil,
    )

    io.println("")
    io.println("html_to_writerly will try to convert " <> path <> " to .wly 👇")
    io.println("")

    let parameters =
      vr.RendererParameters(input_dir: path, output_dir: ".", prettifier_behavior: vr.PrettifierOff)
      |> vr.amend_renderer_paramaters_by_command_line_amendments(amendments)

    let renderer =
      vr.Renderer(
        assembler: html_purifying_assembler,
        parser: vr.default_html_parser(_, amendments.only_key_values),
        pipeline: html_pipeline.html_pipeline(),
        splitter: fn(vxml) { splitter(vxml, file) },
        emitter: fn(fragment) { emitter(fragment, prev, next) },
        writer: vr.default_writer,
        prettifier: vr.empty_prettifier,
      )
      |> vr.amend_renderer_by_command_line_amendments(amendments)

    let renderer_options =
      vr.vanilla_options()
      |> vr.amend_renderer_options_by_command_line_amendments(amendments)

    case vr.run_renderer(renderer, parameters, renderer_options) {
      Ok(_) -> Nil
      Error(error) -> {
        io.println("\nrenderer error on path " <> path <> ":")
        io.println(ins(error))
      }
    }
  })

  io.println("")
}
